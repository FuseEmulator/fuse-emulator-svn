dnl Process this file with autoconf to produce a configure script.
dnl Copyright (c) 1999-2002 Philip Kendall

dnl $Id$

dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
dnl
dnl Author contact information:
dnl
dnl E-mail: pak21-fuse@srcf.ucam.org
dnl Postal address: 15 Crescent Road, Wokingham, Berks, RG40 2DB, England

AC_INIT(libspectrum.c)
AM_CONFIG_HEADER(config.h)

dnl Use automake to produce `Makefile.in'
AM_INIT_AUTOMAKE(libspectrum, 0.1.0)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_RANLIB

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Check sizes of various types; second parameter is a `default' for
dnl cross-compiling. Will obviously break everything if this is wrong...
AC_CHECK_SIZEOF(char, 1)
AC_CHECK_SIZEOF(short, 2)
AC_CHECK_SIZEOF(int, 4)
AC_CHECK_SIZEOF(long, 4)

# Test for glib
AC_MSG_CHECKING(whether to use glib)
AC_ARG_WITH(glib,
[  --without-glib          don't use glib],
if test "$withval" = no; then glib=no; else glib=yes; fi,
glib=yes)
AC_MSG_RESULT($glib)
if test "$glib" = yes; then
  AM_PATH_GLIB(1.2.0, AC_DEFINE(HAVE_LIB_GLIB),
  	       AC_MSG_WARN(glib not found - using internal replacement))
fi

dnl Check for zlib
AC_CHECK_HEADERS(zlib.h)

dnl If it appears we're using gcc as our compiler, turn on warnings
if test "$ac_cv_prog_gcc" = yes; then
  CFLAGS="$CFLAGS -Wall"
  dnl And possibly lots of warnings
  AC_MSG_CHECKING(whether lots of warnings requested)
  AC_ARG_ENABLE(warnings,
  [  --enable-warnings       give lots of warnings if using gcc],
  if test "$enableval" = yes; then
    warnings=yes;
  else
    warnings=no;
  fi,
  warnings=no)
  AC_MSG_RESULT($warnings)
  if test "$warnings" = yes; then
      CFLAGS="$CFLAGS -Wstrict-prototypes -Wmissing-prototypes -Winline -Wshadow -Wpointer-arith -Wcast-qual -Wcast-align -Wwrite-strings -Waggregate-return -W -Wsign-compare"
  fi
fi

AC_OUTPUT(Makefile)
