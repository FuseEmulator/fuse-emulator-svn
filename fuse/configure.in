dnl Process this file with autoconf to produce a configure script.
dnl $Id$

dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
dnl
dnl Author contact information:
dnl
dnl E-mail: pak@ast.cam.ac.uk
dnl Postal address: 15 Crescent Road, Wokingham, Berks, RG40 2DB, England

AC_INIT(fuse.c)
AM_CONFIG_HEADER(config.h)

dnl Use automake to produce `Makefile.in'
AM_INIT_AUTOMAKE(fuse, 0.2.1)

dnl Checks for programs.
AC_PROG_CC

dnl Check for big endianness
AC_C_BIGENDIAN

dnl Find the X libraries
AC_PATH_XTRA
CFLAGS="$CFLAGS $X_CFLAGS";
LDFLAGS="$LDFLAGS $X_LIBS";
LIBS="$LIBS $X_PRE_LIBS -lX11 $X_EXTRA_LIBS";

dnl This apparently needed for SVR4 systems to link correctly
dnl Basically the same as a part of AC_PATH_XTRA, but using `gethostname'
dnl instead of `gethostbyname'.
dnl Thanks to Erik Kunze for pointing out this problem
AC_CHECK_FUNC(gethostname)
if test $ac_cv_func_gethostname = no; then
  AC_CHECK_LIB(nsl, gethostname, X_EXTRA_LIBS="$X_EXTRA_LIBS -lnsl")
fi
AC_CHECK_FUNC(connect)
if test $ac_cv_func_connect = no; then
  AC_CHECK_LIB(socket, connect, X_EXTRA_LIBS="-lsocket $X_EXTRA_LIBS", ,
	$X_EXTRA_LIBS)
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/time.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Checks for library functions.
AC_CHECK_FUNCS(setitimer)

dnl Check for libraries
AC_CHECK_LIB(glib, g_slist_append)
if test $ac_cv_lib_glib_g_slist_append = yes; then
    CFLAGS="$CFLAGS `glib-config --cflags`"
fi

dnl Do we want issue 2 keyboard emulation?
AC_MSG_CHECKING(whether Issue 2 emulation requested)
AC_ARG_ENABLE(issue2,
[  --enable-issue2         emulate an Issue 2 Spectrum],
if test "$enableval" = yes; then
    issue2=yes;
    AC_DEFINE(ISSUE2)
else
    issue2=no;
fi,
issue2=no)
AC_MSG_RESULT($issue2)

AC_SUBST(X_CFLAGS)
AC_SUBST(X_LIBS)
AC_SUBST(X_EXTRA_LIBS)
AC_SUBST(X_PRE_LIBS)

AC_OUTPUT(Makefile)
